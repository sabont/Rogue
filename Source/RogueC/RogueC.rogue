$define ROGUE_RELEASE_VERSION "1.12.2"
$define ROGUE_RELEASE_DATE "January 3, 2022"

$include "Version.rogue" [optional]

# TODO:
# - Generate error on "local v = null"
# - Warn on assignment of intermediate value compound
# - Cannot augment auto-constructor e.g. "class TypeName(filename:String)"; missing <insert> and <append> labels

# Some essential methods to support metacode
$essential String[].count
$essential String[].get

$essential Real64.*
$essential Int64.*
$essential Int32.*
$essential Character.*
$essential Byte.*
$essential Logical.*
$essential Date.*
$essential Time.*

$include "Attributes.rogue"
$include "CPPWriter.rogue"
$include "CythonPlugin.rogue"
$include "PythonPlugin.rogue"
$include "CloneArgs.rogue"
$include "Cmd.rogue"
$include "Local.rogue"
$include "Method.rogue"
$include "Parser.rogue"
$include "Plugin.rogue"
$include "Preprocessor.rogue"
$include "Program.rogue"
$include "Property.rogue"
$include "RogueError.rogue"
$include "Scope.rogue"
$include "State.rogue"
$include "Template.rogue"
$include "Token.rogue"
$include "TokenReader.rogue"
$include "TokenType.rogue"
$include "Tokenizer.rogue"
$include "Type.rogue"
$include "Visitor.rogue"
$include "Visitors.rogue"

RogueC.launch


class GCMode
  ENUMERATE
    MANUAL
    AUTO_ST
    AUTO_MT
    BOEHM
    BOEHM_TYPED
endClass

enum ThreadMode
  CATEGORIES
    NONE
    PTHREADS
    CPP
endEnum


class RogueC [singleton]
  PROPERTIES
    included_files     = String[]
    prefix_path_list   = String[]
    prefix_path_lookup = Table<<String,Logical>>()
    compile_targets    = Table<<String,Logical>>()
    target_list        = String[]
    libraries_folder   = ""

    native_libraries = StringLookupList()

    source_files    = String[]
    generate_main   : Logical
    first_filepath  : String
    output_filepath : String
    exe_filepath    : String

$if defined(DEFAULT_TARGETS)
    default_targets = DEFAULT_TARGETS
$else
    default_targets = ["C++"]
$endIf
    supported_targets      = String[]
    essential_declarations = String[]

    all_essential = false
    # If true everything is essential

    all_api = false
    # If true everything is marked as an API

    essential_files        = Set<<String>>()
    # Everything in these files will be marked [essential]

    api_files        = Set<<String>>()
    # Everything in these files will be marked [api]

    manual_files_essential = false
    # If true, all files in command line will be essential

    api_classes      = Set<<String>>()
    # These classes will be marked [api]

    api_file_patterns = String[]
    api_class_patterns = String[]

    manual_files_api = false
    # If true, all files in command line will be api

    all_methods_callable_dynamically = false
    # When true, all methods can be called dynamically. Set by the Python and Cython plug-ins.

    todo_keywords = ["FIXME"]
    # Lines with comments containing the given keywords will be printed during compilation.

    omit_line_numbers = false
    # If true, don't emit #line directives

    quiet = false
    # Suppress non-error output.

    compile_output    : Logical
    compiler_name     : String
    compiler_options  = String[]
    execute_args      : String
    pkg_config_pkgs   = String[]

    package_name      : String

    debug_mode        : Logical
    release_mode      : Logical
    run_tests         : Logical
    should_print_version : Logical

    parsers = Parser[]

    scanners_by_filepath = Table<<String,Scanner>>()
    stopwatch : Stopwatch

    gc_mode = GCMode.AUTO_ST : Int32
    gc_threshold = 1024*1024 : Int32
    gc_mode_set = false

    thread_mode = ThreadMode.NONE

    plugins = Plugin[]

    using_ide : String

  METHODS
    method init
      plugins.add(
        Plugin( "Metacode" ) subclass
          METHODS
            # method collect_supported_targets
            #   UNDEFINED - this happens too early before any metacode has had a chance to hook in
            method generate_additional_types->Logical
              local result = Program.invoke_metacode( "RogueC.generate_additional_types", Program )
              if (result.is_list)
                if ((forEach in result)->Logical) return true
                return false
              else
                return result->Logical
              endIf
            method start_code_file( code_object:Object )
              Program.invoke_metacode( "RogueC.start_code_file", code_object )
            method finish_code_file( code_object:Object )
              Program.invoke_metacode( "RogueC.finish_code_file", code_object )
            method start_header_file( code_object:Object )
              Program.invoke_metacode( "RogueC.start_header_file", code_object )
            method finish_header_file( code_object:Object )
              Program.invoke_metacode( "RogueC.finish_header_file", code_object )
        endSubclass
      )

    method launch
      local libs = File.path( System.executable_filepath )
      if (not File.exists(libs/"Libraries")) libs = File.parent( libs )
      libs = libs/"Libraries/"
      if (File.exists(libs))
        libraries_folder = libs
        prefix_path_list.add( libs )
        prefix_path_lookup[ libs ] = true
      endIf

      try
        process_command_line_arguments

        <collect_supported_targets>
        forEach (plugin in plugins) plugin.collect_supported_targets

        if (source_files.count == 0)
          if (should_print_version)
            # Print version and date only and exit
            print_version
            System.exit( 0 )
          endIf

          println
          print   "Rogue Compiler "
          print_version

          local usage =
                  @|
                   |USAGE
                   |  roguec [options] file1.rogue [file2.rogue ...]
                   |
                   |OPTIONS
                   |  --api[=<class, filepath, or wildcard pattern>]
                   |    Give one or more classes or the contents of one or more files the [api] attribute
                   |    which gives all methods of that class the [essential] attribute and prevents the
                   |    compiler from culling unused methods. This is useful for both invocation by
                   |    external code and for introspection. If a class is never referenced
                   |    then [api] will not cause it to be included in the generated code - use
                   |    --essential/[essential] for that.
                   |
                   |    The --api flag and the class-level [api] attribute will ignore any methods marked with
                   |    the [noAPI] attribute.
                   |
                   |    VARIATIONS
                   |      --api
                   |        Applies [api] to all filenames included on the command line.
                   |      --api=<ClassName>
                   |        Gives the specified class the [api] attribute.
                   |      --api=<Filepath.rogue>
                   |        Includes the given file and marks its content as [api].
                   |
                   |      A wildcard pattern can be used to specify classes and filepaths. If the pattern
                   |      ends with '.rogue' it is treated as a filepath pattern and otherwise as a
                   |      class name pattern. Unlike the behavior of an explicit filepath, filepath
                   |      patterns do not actually include the given files, they only match filepaths that
                   |      are included on the command line or in Rogue source.
                   |
                   |        *   - Match any characters but does not span folder separators (/).
                   |        **  - Match any characters, spanning folder separators.
                   |        ?   - Match any single character.
                   |
                   |    See also: --essential
                   |
                   |  --compile[=<compiler invocation>]
                   |    Creates an executable from the compiled .rogue code - for example, compiles
                   |    and links the .cpp code generated from the .rogue program.  Automatically
                   |    enables the --main option.  If <compiler invocation> is omitted then a
                   |    language-specific default is used - for a C++ code target this is the
                   |    Makefile-default $(CXX) compiler with certain options - see DEFAULT_CXX in
                   |    the Rogue source folder's Makefile.
                   |
                   |  --compile-arg[=<addendum to compiler invocation>]
                   |    Like --compile, this passes the output of compiling the .rogue code to, e.g,
                   |    the C++ compiler.  There are two differences.  First, it does not imply
                   |    the --main option.  Second, the argument is appended to the current compiler
                   |    command line rather than replacing it.  You may specify this more than once,
                   |    and you may specify it in conjunction with --compile.
                   |
                   |  --debug
                   |    Enables exception stack traces.
                   |
                   |  --define="name[:value]"
                   |    Adds a single preprocessor define.
                   |    Defining "name:value" is equivalent to: $define name value
                   |    Defining "name" is equivalent to:       $define name true
                   |
                   |  --essential=[ClassName|ClassName.method_name(ParamType1,ParamType2,...)],...
                   |    Makes the given class or method essential ("do not cull if unused").
                   |    Certain wildcard patterns may be used:
                   |      --essential=*               - All classes (but not methods)
                   |      --essential=**              - All classes and all methods ("exhaustive")
                   |      --essential=Type.*          - All methods of 'Type'
                   |      --essential=Type.m          - All methods of 'Type' named 'm'
                   |      --essential=<pattern>.rogue - Includes files and marks content [essential]
                   |      --essential                 - All filenames on the command line made [essential]
                   |    See also: --api
                   |
                   |  --exe=<filepath>
                   |    Specify the destination folder and/or filename for the compiled
                   |    executable. Only used with --compile or --execute. See also: --output
                   |
                   |  --execute[="args"]
                   |    Use command line directives to compile and run the output of the
                   |    compiled .rogue program.  Automatically enables the --main option.
                   |
                   |  --gc[=auto|auto-mt|manual|boehm|boehm-typed]
                   |    Set the garbage collection mode:
                   |      --gc=auto        - Rogue collects garbage as it executes.  Slower than
                   |                         'manual' without optimizations enabled.
                   |      --gc=auto-mt     - Like auto, but works with multithreading (i.e., when
                   |                         the --threads option is not 'none').
                   |      --gc=manual      - Rogue_collect_garbage() must be manually called
                   |                         in-between calls into the Rogue runtime.
                   |      --gc=boehm       - Uses the Boehm garbage collector.  The Boehm's GC
                   |                         library must be obtained separately and linked in.
                   |      --gc=boehm-typed - Like boehm, but provides type info to the collector.
                   |
                   |  --gc-threshold={number}[MB|K]
                   |    Specifies the default garbage collection threshold of the compiled program.
                   |    Default is 1MB.  If neither MB nor K is specified then the number is
                   |    assumed to be bytes.
                   |
                   |  --help
                   |    Shows help (you're reading it).
                   |
                   |  --ide[=<IDE Name>]
                   |    Indicates that roguec is being invoked from an IDE.  Currently this causes
                   |    error messages to print out in a "classic" C style, which Xcode (and
                   |    possibly other IDEs) will automatically pick up and display.
                   |
                   |  --libraries="path1[;path2...]"
                   |    Add one or more additional library folders to the search path.
                   |
                   |  --main
                   |    Include a main() function in the output file.
                   |
                   |  --output=<filepath>
                   |    Specify the destination folder and/or the base filename for generated C++
                   |    output. See also: --exe
                   |
                   |  --pkg-config=<pkg>
                   |    Gathers compiler flags for the given package from pkg-config and passes
                   |    them to the backend compiler.  Can be specified more than once.  Only
                   |    works with the C++ target.
                   |
                   |  --quiet
                   |    Suppresses normal compilation output. Errors are still displayed.
                   |
                   |  --release
                   |    Compiles generated C++ code with full optimization enabled. Only takes
                   |    effect if --compile or --execute are specified.
                   |
                   |  --target=

                   # --target info filled in below

                   #|  --package=com.developer.app [Java only]

                   #|  --no-line-mapping
                   #|    Do not attempt to map Rogue line numbers to target code line numbers.
                   #|    (Mostly useful for debugging compiler errors.)
                   #|

          usage += supported_targets->String.replacing("]",",...]\n")

          usage += @|    Specify one or more compile targets that are used with conditional code
                    |    compilation ('$if target("C++") ...').  C++ is the only supported
                    |    language target.  "Python" and "Cython" are optional plug-in targets.
                    |    Arbitrary platforms may be added at will, e.g. "iOS", "macOS", etc.
                    |    Default:

          usage += " " + ",".join(default_targets) + "\n"

          usage += @|
                    |  --test
                    |    Compiled program includes any defined unit tests and runs them on launch.
                    |    Enables --debug automatically.
                    |
                    |  --threads=[none|pthreads|cpp]
                    |    Set the threading mode.  Note that if you enable multithreading, you
                    |    likely want to adjust the --gc mode also (e.g., to auto-mt or boehm).
                    |    Leaving this unspecified defauts to none.  --threads by itself implies
                    |    pthreads.  Specifying it expicitly, the mode may be one of:
                    |      --threads=none     - No multithreading support. (Default)
                    |      --threads=pthreads - Multithreading based on pthreads.
                    |      --threads=cpp        Multithreading based on C++ std::thread.
                    |
                    |  --todo[="KEYWORD"]
                    |    During compilation the compiler will print out any lines containing a
                    |    comment with the word TODO or other specified keyword.  May be specified
                    |    more than once.  Lines containing a FIXME comment are always printed.
                    |
                    |  --version
                    |    Print the version number and release date.
          println usage
          System.exit( 1 )
        endIf

        if (target_list.count == 0) target_list = default_targets.cloned
        <validate_target_list>

        forEach (target in target_list)
          compile_targets[target] = true
        endForEach

        Program.configure

        Preprocessor.define( "DEBUG", debug_mode )
        Preprocessor.define( "THREAD_MODE", thread_mode->String )

        stopwatch = Stopwatch()

        first_filepath = source_files.first
        include_source( "Standard$NativeCode.rogue" (File.separator), &do_not_save_prefix )
        forEach (filepath in source_files)
          include_source( filepath, &from_command_line )
        endForEach
        include_source( "Standard" )

        parse_pending_files

        # Add essential declarations to Program's essential list
        local t = TokenType("Internal").create_token( "[Command Line]", 0, 0 )
        forEach (declaration in essential_declarations)
          Program.get_on_launch_method( t ).statements.add( CmdMakeRequisite(t,declaration) )
        endForEach

        Program.resolve
        local type_TypeInfo = Program.find_type( "TypeInfo" )
        if (type_TypeInfo) type_TypeInfo.simplify_name = true  # TODO: add general mechanism for having classes simplify their names

        if (thread_mode != ThreadMode.NONE and not gc_mode_set)
          Console.error.println "NOTE: When specifying --threads, you should also specify a --gc mode."
        endIf

        write_output

      catch (err:RogueError)
        if (using_ide) Console.error.println "$:$: error:$" (err.filepath,err.line,err.message)
        else           println err
        System.exit(1)

      catch (err:Error)
        if (using_ide) Console.error.println "(Unknown Location):0: error:$" (err.message)
        else           println err
        System.exit(1)
      endTry

    method parse( filepath:String, source:String )
      parsers.add( Parser(filepath,source) )
      parse_pending_files

    method parse_pending_files
      # One parser is created for every file and all parsers are prepped with tokens
      # before any parser parses its elements.
      # This allows included $defines to be collected before the including files are parsed.
      # This is also where most identifier tokens are qualified with their module namespace
      # for 'module' and 'using' directives.
      # 'includeSource' DEPENDENCIES can cause additional files to be parsed later
      # so we guard against that by working on a copy of parsers[] and clearing parsers[].

      # Start out by creating a new parser for each newly instanced module template
      while (Program.module_instance_list.count)
        local modules = Program.module_instance_list
        Program.module_instance_list = ModuleInstance[]
        forEach (module_template in modules)
          parsers.add( module_template.instantiate )
        endForEach
      endWhile

      while (parsers.count)
        local parsers = this.parsers.cloned
        this.parsers.clear
        forEach (parser in parsers) parser.insert_module_prefixes
        forEach (parser in parsers) parser.parse_elements
      endWhile
      Program.types_resolved = false

    method write_output
      <target_specific_code>
      throw RogueError( ''Compile target "$" is not supported.'' (target_list) )

    method on_compile_finished
      if (not quiet) println "SUCCESS ($)" (stopwatch)

    method set_include_attributes( t:Token, filepath:String, attributes:IncludeAttributes, &including_folder )
      local file = find_source_file( t, filepath, &=including_folder )

      if (file is null) return  # error will be generated later if necessary

      if (file.is_folder)
        # Set include attributes for each .rogue file in the folder (non-recursive)
        forEach (folder_filepath in Files(file.filepath,"*.rogue"))
          set_include_attributes( t, folder_filepath, attributes )
        endForEach
        return
      endIf

      if (included_files.contains(file.filepath))
        if (attributes.is_essential and not essential_files.contains(file.filepath))
          if (t is null) t = TokenType.meta_include.create_token( "[Command Line]", 0, 0 )
          throw t.error( ''"$" was already included without [essential] and cannot be retroactively processed."'' (filepath) )
        endIf
        if (attributes.is_api and not api_files.contains(file.filepath))
          if (t is null) t = TokenType.meta_include.create_token( "[Command Line]", 0, 0 )
          throw t.error( ''"$" was already included without [api] and cannot be retroactively processed."'' (filepath) )
        endIf
      else
        if (attributes.is_essential) essential_files.add( file.filepath )
        if (attributes.is_api)       api_files.add( file.filepath )
      endIf

    method find_source_file( t:Token, filepath:String, &including_folder, &is_optional, &do_not_save_prefix )->File
      if (t)
        # Try to fit new filepath onto end of context filepath.
        local context_path = t.filepath.replacing('\\','/').split('/')
        context_path.remove_last
        local include_path = filepath.replacing('\\','/').split('/')
        local filename = include_path.remove_last
        local new_path = context_path.cloned
        while (context_path.count and include_path.count and context_path.last == include_path.last)
          include_path.remove_last
          context_path.remove_last
        endWhile
        if (include_path.count == 0)
          new_path.add( filename )
          local new_filepath = File.join(new_path)
          if (File.exists(new_filepath) and (not File.is_folder(new_filepath) or including_folder)) filepath = new_filepath
        endIf
      endIf

      local file = File(filepath)

      if (File.exists(filepath+".rogue"))
        filepath += ".rogue"
        file = File( filepath )
      endIf
      if (not file.exists or (file.is_folder and not including_folder))
        forEach (prefix in prefix_path_list)
          local prefixed_filepath = File.join(prefix, filepath)
          local prefixed_file = File( prefixed_filepath )
          if (File.exists(prefixed_filepath+".rogue"))
            filepath = prefixed_filepath + ".rogue"
            file = File( filepath )
            escapeForEach
          elseIf (prefixed_file.exists)
            filepath = prefixed_filepath
            file = prefixed_file
            escapeForEach
          endIf
        endForEach
      endIf

      if (not file.exists)
        return null
      endIf

      if (file.is_folder)
        if (including_folder) return File( file.absolute_filepath )
        local path2 = File.join(filepath, "$.rogue" (file.filename))
        local file2 = File( path2 )
        if (not file2.exists)
          if (is_optional)
            if (not do_not_save_prefix) _save_search_path( file.filepath + File.separator )
            return null
          endIf
          if (t is null) t = TokenType.meta_include.create_token( "[Command Line]", 0, 0 )
          throw t.error( "'$include' used on a folder - use '$includeFolder' instead." )
        elseIf (file2.is_folder)
          return null
        else
          if (not prefix_path_lookup.contains(File.ensure_ends_with_separator(file.filepath)))
            prefix_path_lookup[file.filepath] = true
            prefix_path_list.add( File.ensure_ends_with_separator(file.filepath) )
          endIf
          filepath = path2
          file = file2
        endIf
        if (t is null) t = TokenType.meta_include.create_token( "[Command Line]", 0, 0 )
      else
        if (including_folder)
          if (is_optional) return null
          if (t is null) t = TokenType.meta_include.create_token( "[Command Line]", 0, 0 )
          throw t.error( "'$includeFolder' used on a source file - use '$include' instead." )
        endIf
      endIf

      return File( file.absolute_filepath )

    method include_source( filepath:String, &is_optional, &do_not_save_prefix, &from_command_line, &including_folder )
      include_source( null, filepath, is_optional, do_not_save_prefix, from_command_line, &=including_folder )

    method include_source( t:Token, filepath:String, &is_optional, &do_not_save_prefix, &from_command_line, &including_folder )->Logical
      # Preemptively (immediately) parses the given file.
      local file = find_source_file( t, filepath, &=including_folder, &=is_optional, &=do_not_save_prefix )
      if (file is null)
        if (is_optional) return false
        local mesg = which{ including_folder:''No such folder "$".''(filepath) || ''No such file "$".'' (filepath) }
        if (t) throw t.error( mesg )
        else   throw RogueError( mesg )
      endIf

      if (file.is_folder)
        # $include each .rogue file in the folder (non-recursive)
        forEach (folder_filepath in Files(file.filepath,"*.rogue"))
          include_source( t, folder_filepath, &=is_optional, &=do_not_save_prefix, &=from_command_line )
        endForEach
        return true
      endIf

      filepath = file.filepath
      if (included_files.contains(filepath)) return false

      included_files.add( filepath )
      if (from_command_line)
        if (manual_files_api) api_files.add( filepath )
        if (manual_files_essential) essential_files.add( filepath )
      endIf

      if (not do_not_save_prefix) _save_search_path( File.folder(filepath) + File.separator )

      parsers.add( Parser( filepath ) )

      return true

    method _save_search_path( filepath:String )
      if (not prefix_path_lookup.contains(filepath))
        prefix_path_lookup[filepath] = true
        prefix_path_list.add( filepath )
      endIf

    method include_native( t:Token, filepath:String, native_type:String, is_optional:Logical )->Logical
      local file = File(filepath)

      if (not file.exists)
        forEach (prefix in prefix_path_list)
          local prefixed_filepath = prefix + filepath
          local prefixed_file = File( prefixed_filepath )
          if (prefixed_file.exists)
            filepath = prefixed_filepath
            file = prefixed_file
            escapeForEach
          endIf
        endForEach
      endIf

      if (not file.exists)
        if (is_optional) return false
        local mesg = ''No such file "$".'' (filepath)
        if (t) throw t.error( mesg )
        else   throw RogueError( mesg )
      endIf

      if (file.is_folder)
        local mesg = ''Cannot target folder "$" for a native include.'' (file.filepath)
        if (t) throw t.error( mesg )
        else   throw RogueError( mesg )
      endIf

      filepath = file.absolute_filepath
      if (included_files.contains(filepath)) return true

      included_files.add( filepath )

      local prefix = File.folder(filepath) + File.separator
      if (not prefix_path_lookup.contains(prefix))
        prefix_path_lookup[prefix] = true
        prefix_path_list.add( prefix )
      endIf

      if (native_type == "header")
        forEach (line in LineReader(file))
          Program.native_header.add( line )
        endForEach
      else
        forEach (line in LineReader(file))
          Program.native_code.add( line )
        endForEach
      endIf

      return true

    method print_version
      print( 'v' ).print( ROGUE_RELEASE_VERSION )
      #{
      print( " (" )
$if defined(ROGUE_RELEASE_BUILD)
        print( ROGUE_RELEASE_BUILD )
        print( ' ' )
        print( ROGUE_RELEASE_COMMIT_ID )
$else
        print "develop"
$endIf
      print( ')' )
      }#
      println
      println ROGUE_RELEASE_DATE

    method process_command_line_arguments
      forEach (arg in System.command_line_arguments)
        local value = arg.after_first('=')
        arg = arg.before_first('=')
        if (arg.begins_with("--"))
          # Option
          which (arg)
            case "--help"
              source_files.clear
              return

            case "--output"
              if (value.count == 0) throw RogueError( ''Output filepath expected after "--output=".'' )
              output_filepath = value

            case "--exe"
              if (value.count == 0) throw RogueError( ''Executable filepath expected after "--exe=".'' )
              exe_filepath = value

            case "--compile"
              generate_main = true
              compile_output = true
              if (value.count) compiler_name = value

            case "--compile-arg"
              compile_output = true
              if (value.count) compiler_options.add( value )

            case "--pkg-config"
              compile_output = true
              if (value.count == 0) throw RogueError( ''Expected a package name after "--pkg-config=".'' )
              pkg_config_pkgs.add( value )

            case "--quiet"
              quiet = true

            case "--execute"
              generate_main = true
              compile_output = true
              execute_args = value

            case "--debug"
              debug_mode = true

            case "--define"
              if (not value.count) throw RogueError( ''Expected "name" or "name:value" after "--define=".'' )
              local i = value.locate( ':' )
              if (not i)
                Preprocessor.define( value, "true" )
              else
                Preprocessor.define( value.before_first(':'), value.after_first(':') )
              endIf

            case "--ide"
              if (value.count)
                using_ide = value
              else
                using_ide = "Unknown"
              endIf

            case "--main"
              if (value.count) throw RogueError( "Unexpected value for '--main' option." )
              generate_main = true

            case "--libraries"
              if (not value.count) throw RogueError( ''One or more paths expected after "--libraries=".'' )
              forEach (path in value.split(';'))
                if (File.exists(path))
                  if (File.is_folder(path))
                    path = File.ensure_ends_with_separator( path )
                    if (not prefix_path_lookup.contains(path))
                      prefix_path_list.add( path )
                      prefix_path_lookup[ path ] = true
                    endIf
                  else
                    source_files.add( path )
                  endIf
                endIf
              endForEach

            case "--main"
              if (value.count) throw RogueError( "Unexpected value for '--main' option." )
              generate_main = true

            case "--no-line-mapping"
              omit_line_numbers = true

            case "--package"
              if (not value.count)
                throw RogueError( ''Java package name expected after "--package=" (e.g. "com.developer.app").'' )
              endIf
              package_name = value

            case "--release"
              release_mode = true
              if (value.count) throw RogueError( "Unexpected value for '--release' option." )

            case "--api"
              if (value.count)
                local is_pattern = value.contains('*') or value.contains('?')
                if (is_pattern)
                  if (value == "*" or value == "**") all_api = true
                  elseIf (value.ends_with(".rogue")) api_file_patterns.add( "**/" + value )
                  else                               api_class_patterns.add( value )
                elseIf (value.ends_with(".rogue"))
                  local file = find_source_file(null,value)
                  if (file is null)
                    throw RogueError( ''Can not find file "$" for "--api=<filepath>".'' (value) )
                  endIf
                  api_files.add( file.filepath )
                  source_files.add( file.filepath )
                else
                  api_classes.add( value )
                endIf
              else
                manual_files_api = true
              endIf

            case "--essential"
              if (value.count == 0)
                manual_files_essential = true
              elseIf (value == "*")
                all_essential = true
                all_essential = true
              elseIf (value == "**")
                all_essential = true
                all_api = true
              elseIf (value.ends_with(".rogue"))
                local file = find_source_file(null,value)
                if (file is null)
                  throw RogueError( ''Can not find file "$" for "--essential=<filepath>".'' (value) )
                endIf
                essential_files.add( file.filepath )
                source_files.add( file.filepath )
              else
                essential_declarations.add( value.split(',') )
              endIf

            case "--target"
              if (not value.count)
                throw RogueError( ''One or more comma-separated target names expected after "--target=" (e.g. "C++").'' )
              endIf
              target_list.add( value.split(',') )

            case "--todo"
              if (value.count) todo_keywords.add( value )
              else             todo_keywords.add( "TODO" )

            case "--gc"
              gc_mode_set = true
              if ((not value.count) or value == "auto")
                # Default to AUTO if nothing specified
                gc_mode = GCMode.AUTO_ST
              elseIf (value == "auto-mt")
                gc_mode = GCMode.AUTO_MT
              elseIf (value == "manual")
                gc_mode = GCMode.MANUAL
              elseIf (value == "boehm")
                gc_mode = GCMode.BOEHM
              elseIf (value == "boehm-typed")
                gc_mode = GCMode.BOEHM_TYPED
              else
                throw RogueError( 'Unknown GC mode (--gc=$)' (value) )
              endIf

            case "--gc-threshold"
              if (not value.count)
                throw RogueError( ''A value such as 1.1MB, 512K, or 65536 expected after "--gc-threshold=".'' )
              endIf
              value = value.to_lowercase
              local n = value->Real64
              if (value.ends_with('m') or value.ends_with("mb")) n *= 1024*1024
              elseIf (value.ends_with('k') or value.ends_with("kb")) n *= 1024
              local thresh = n->Int32
              if (thresh < 1) thresh = 0x7fffffff
              gc_threshold = thresh

            case "--threads"
              if ((not value.count) or value == "pthreads")
                # Default to pthreads if nothing specified
                thread_mode = ThreadMode.PTHREADS
              elseIf (value == "cpp")
                thread_mode = ThreadMode.CPP
              elseIf (value == "none")
                thread_mode = ThreadMode.NONE
              else
                throw RogueError( 'Unknown threads mode (--threads=$)' (value) )
              endIf

            case "--plugin-test"
              plugins.add(
                Plugin( "Test" ).on_generate_additional_types(
                  function->Logical
                    @trace "All types resolved - generate additional types?"
                    if (not Program.find_type("Alpha"))
                      local src = @|class Alpha [essential singleton]
                                   |  METHODS
                                   |    method init
                                   |      @trace "Alpha singleton instantiated!"
                                   |endClass
                      println "Yes - compiling type Alpha:"
                      println src
                      RogueC.parse( "(compiler-generated)", src )
                      return true
                    else
                      println "No"
                      return false
                    endIf
                  endFunction
                )
              )

            case "--test"
              run_tests = true
              debug_mode = true

            case "--version"
              if (value.count)
                throw RogueError( ''Unexpected argument to --version option.'' )
              endIf
              should_print_version = true

            others
              throw RogueError( "Unknown option '$'." (arg) )

          endWhich
        else
          # Input Filename
          source_files.add( arg )
        endIf
      endForEach

    method require_valueless( arg:String, expecting:String )
      if (arg == expecting) return
      if (arg.contains('=')) throw RogueError( "Unexpected value for command line argument '$'." (expecting) )

endClass

#{
                   |      manual - A GC cannot happen while Rogue-generated code is executing.
                   |               After control returns to C++, the main loop must call either
                   |               Rogue_collect_garbage() or Rogue_update_tasks() which will allow
                   |               the GC to run if it needs to.  Manual execution is typically
                   |               just slightly faster than auto.
                   |
                   |      auto   - Generated code code automatically checks for and performs a GC if
                   |               necessary as it runs and allocates objects.  Generated auto-GC
                   |               code should be compiled with -O3 to make its speed acceptable.
                   |
                   }#
