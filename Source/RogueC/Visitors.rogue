class DependencyInjectionVisitor : Visitor
  PROPERTIES
    injected_dependencies : Logical

  METHODS
    method inject_dependencies( statements:CmdStatementList )->Logical
      statements.dispatch( this )
      return injected_dependencies

    method on_enter( cmd:CmdNativeCode )
      if (cmd.code)
        Program.native_code.add( cmd.code )
        injected_dependencies = true
        cmd.code = null
      endIf

    method on_enter( cmd:CmdNativeHeader )
      if (cmd.code)
        Program.native_header.add( cmd.code )
        injected_dependencies = true
        cmd.code = null
      endIf

    method on_enter( cmd:CmdNativeLibrary )
      if (cmd.library)
        RogueC.native_libraries.add( cmd.library )
        injected_dependencies = true
        cmd.library = null
      endIf

    method on_enter( cmd:CmdIncludeNative )
      if (cmd.filepath)
        if (RogueC.include_native( cmd.t, cmd.filepath, cmd.file_type, cmd.is_optional ))
          injected_dependencies = true
        endIf
        cmd.filepath = null
      endIf

    method on_enter( cmd:CmdMakeRequisite )
      if (not cmd.pattern) return
      injected_dependencies = true

      local t = cmd.t
      local pattern = cmd.pattern
      if (pattern.contains('.'))
        local req_type = Program.get_type_reference( t, pattern.before_first('.') )
        req_type.make_essential.organize
        local sig = pattern.after_first('.')

        if (sig == "*")
          forEach (r in req_type.global_method_list)
            if (not r.is_deprecated) r.make_essential
          endForEach
          forEach (m in req_type.method_list)
            if (not m.is_deprecated) m.make_essential
          endForEach

        elseIf (sig.contains('('))
          local m = req_type.find_method( sig )
          if (not m and req_type.method_templates.has_template(sig.before_first('(')))
            # Try again after instantiating new method templates
            req_type.method_templates.instantiate( t, sig.before_first('('), null )
            m = req_type.find_method( sig )
          endIf

          if (m) m.make_essential
          else   throw t.error( "No such method $ in type $." (sig,req_type.name) )

        else
          local list = req_type.global_method_lookup_by_name[ sig ]
          if (list)
            forEach (m in list) m.make_essential
          endIf

          list = req_type.method_lookup_by_name[ sig ]
          if (list)
            forEach (m in list) m.make_essential
          endIf
        endIf

      else
        local req_type = Program.get_type_reference( t, pattern )
        req_type.make_essential.organize
      endIf

      cmd.pattern = null
endClass



class QueryToGenericFunctionConversionVisitor : Visitor [singleton]
  PROPERTIES
    cmd_create_fn : CmdCreateFunction

  METHODS
    method convert( cmd:Cmd, cmd_create_fn )
      cmd_create_fn.statements.add( cmd.dispatch(this) )

    method convert_to_fn_with_arg( cmd:Cmd )->Cmd
      ensure cmd_create_fn.with_args
      local name = "p" + cmd_create_fn.with_args.count + 1
      cmd_create_fn.with_args.add( FnArg(name,cmd) )
      return CmdAccess( cmd.t, name )

    method visit( cmd:CmdAccess )->Cmd
      if (ContainsImplicitAccessVisitor.check(cmd))
        return prior.visit( cmd )
      else
        return convert_to_fn_with_arg( cmd )
      endIf

    method visit( cmd:CmdReadLocal )->Cmd
      return convert_to_fn_with_arg( cmd )

    method visit( cmd:CmdThisContext )->Cmd
      return convert_to_fn_with_arg( cmd )

    method visit( cmd:CmdImplicitAccess )->Cmd
      return cmd.convert_to_explicit_access

endClass

class ContainsImplicitAccessVisitor : Visitor [singleton]
  PROPERTIES
    contains_implicit_identifier_access : Logical

  METHODS
    method check( cmd:Cmd )->Logical
      contains_implicit_identifier_access = false
      cmd.dispatch( this )
      return contains_implicit_identifier_access

    method visit( cmd:CmdImplicitAccess )->Cmd
      contains_implicit_identifier_access = true
      return prior.visit( cmd )

endClass


class TraceUsedCodeVisitor : Visitor [singleton]
  METHODS
    method on_enter( type:Type )
      type.trace_used_code

    method on_enter( m:Method )
      m.trace_used_code

    method on_enter( cmd:CmdLiteralString )
      cmd.index = Program.add_literal_string( cmd.value )

    method on_leave( cmd:CmdCallDynamicMethod )
      if (cmd.method_info.is_overridden) cmd.method_info.called_dynamically = true

    method on_enter( cmd:CmdTypeSelector )
      cmd.of_type.make_essential

    method on_enter( cmd:CmdAssignFnPtr )
      Program.type_TypeInfo.make_essential

endClass


class CollectLocalsVisitor : Visitor
  PROPERTIES
    locals = Local[]

  METHODS
    method collect_locals( m:Method )->Local[]
      locals.add( forEach in m.parameters )
      m.statements.dispatch( this )
      return locals

    method on_enter( cmd:CmdLocalDeclaration )
      locals.add( cmd.local_info )

    method on_enter( cmd:CmdCatch )
      if (cmd.error_var) locals.add( cmd.error_var )
endClass


class UpdateThisTypeVisitor : Visitor
  PROPERTIES
    this_type   : Type
    this_method : Method

  METHODS
    method init( this_type, this_method )
      if (this_method.returns_this)
        this_method.return_type = this_type
      endIf

    method on_enter( cmd:CmdThisContext )
      cmd.this_type = this_type

    method on_enter( cmd:CmdThisType )
      if (cmd.this_type is not this_type)
        cmd.cmd_new = null  # regenerate
        cmd.this_type = this_type
      endIf
endClass


class ConvertReturnToEscapeVisitor : Visitor
  PROPERTIES
    this_method      : Method
    return_type      : Type
    result_name      : String
    return_flag_name : String
    container        : CmdControlStructure

  METHODS
    method init( this_method, result_name, return_flag_name, container )
      return_type = this_method.return_type

    method visit( cmd_return:CmdReturn )->Cmd
      local t = cmd_return.t
      local cmd_block = CmdBlock( t )
      if (return_flag_name)
        cmd_block.statements.add( CmdAssign( t, CmdAccess(t,return_flag_name), CmdLiteralLogical(t,true) ) )
      endIf
      if (return_type)
        if (cmd_return.value)
          cmd_block.statements.add( CmdAssign( t, CmdAccess(t,result_name), cmd_return.value ) )
        else
          throw t.error( "Return value expected." )
        endIf
      else
        if (cmd_return.value)
          local sig = which{ this_method.signature || this_method.name+"()" }
          throw t.error( "$ is not declared as returning a value." (sig) )
        endIf
      endIf
      cmd_block.statements.add( CmdEscape( t, container.control_type, container ) )
      return cmd_block
endClass

class InjectCmdBeforeReturnVisitor( cmd:Cmd ) : Visitor
  PROPERTIES
    depth = 1

  METHODS
    method visit( cmd_escape:CmdEscape )->Cmd
      if (depth > 1) return cmd_escape
      local cmd_block = CmdBlock( cmd_escape.t )
      cmd_block.statements.add( cmd.cloned )
      cmd_block.statements.add( cmd_escape )
      return cmd_block

    method visit( cmd_temporarily:CmdTemporarily )->Cmd
      ++depth
      local result = prior.visit( cmd_temporarily )
      --depth
      return result

    method visit( cmd_try:CmdTry )->Cmd
      if (cmd_try.control_type == CmdControlStructure.type_temporarily)
        ++depth
        local result = prior.visit( cmd_try )
        --depth
        return result
      else
        return prior.visit( cmd_try )
      endIf

    method visit( cmd_return:CmdReturn )->Cmd
      # Transform:
      #   return x
      # Into:
      #   block
      #     local temp = x
      #     cmd
      #     return temp
      #   endBlock
      #
      # Transform:
      #   return
      # Into:
      #   block
      #     cmd
      #     return
      #   endBlock
      local t = cmd_return.t
      local cmd_block = CmdBlock( t )
      if (cmd_return.value)
        local v_temp = Local( t, Program.create_unique_id, null, cmd_return.value )
        cmd_block.statements.add( CmdLocalDeclaration(t,v_temp) )
        cmd_block.statements.add( cmd.cloned )
        cmd_block.statements.add( CmdReturn(cmd_return.t,CmdAccess(t,v_temp)) )
      else
        cmd_block.statements.add( cmd.cloned )
        cmd_block.statements.add( cmd_return )
      endIf
      return cmd_block

    method visit( cmd_throw:CmdThrow )->Cmd
      # Transform:
      #   throw x
      # Into:
      #   block
      #     local temp = x
      #     cmd
      #     throw temp
      #   endBlock
      local t = cmd_throw.t
      local cmd_block = CmdBlock( t )
      local v_temp = Local( t, Program.create_unique_id, null, cmd_throw.expression )
      cmd_block.statements.add( CmdLocalDeclaration(t,v_temp) )
      cmd_block.statements.add( cmd.cloned )
      cmd_block.statements.add( CmdThrow(cmd_throw.t,CmdAccess(t,v_temp)) )
      return cmd_block
endClass

